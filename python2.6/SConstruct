import os, os.path, pdb
import SconsUtils.utils as utils
from functools import partial

Import('env')

nenv = env.Clone()

BOUND_FILES = Split("""
              #include/aff4_constants.h
              #include/aff4_utils.h
              #include/aff4_rdf.h
              #include/aff4_io.h
              #include/aff4_resolver.h
              #include/aff4_rdf_serialise.h
              #include/aff4_objects.h
              #include/aff4_crypto.h
""")

if utils.HEADERS.get("HAVE_LIBEWF_H"):
    BOUND_FILES.append("#include/ewfvolume.h")

import class_parser

def build_python_bindings(target, source, env, initialization=''):
    """ A command to generate python bindings """
    module_name = os.path.splitext(os.path.basename(target[0].name))[0]
    utils.warn("Generating automatic python bindings for module %s" % module_name)

    p = class_parser.HeaderParser(module_name, verbose=env['V'])
    p.module.init_string = initialization
    p.parse_filenames([s.get_abspath() for s in source])

    fd = open(target[0].get_abspath(), 'w')
    p.write(fd)
    fd.close()

nenv.Append(CFLAGS = " -include lib/config.h -Ilibraptor ")

if not nenv.get("disable_curl"):
    BOUND_FILES.append("#include/aff4_http.h")

nenv.Command('pyaff4.c', BOUND_FILES, partial(build_python_bindings,
                                              initialization=''))
nenv.Depends('pyaff4.c', 'class_parser.py')

nenv.PythonModule("pyaff4", ["pyaff4.c"], LIBS=env.libaff4_so)

## Only build those if they exist and are configured:
if utils.HEADERS.get('HAVE_TSK3') and nenv.config.TSK3_HEADER_LOCATION:
    BOUND_FILES = Split("""
    %(TSK3_HEADER_LOCATION)s/libtsk.h
    %(TSK3_HEADER_LOCATION)s/fs/tsk_fs.h
    %(TSK3_HEADER_LOCATION)s/base/tsk_base.h
    %(TSK3_HEADER_LOCATION)s/img/tsk_img.h
    tsk/tsk3.h
    """ % nenv.config.__dict__)

    nenv.Append(CFLAGS = " -I %(TSK3_HEADER_LOCATION)s " % nenv.config.__dict__)

    nenv.Command('tsk/pytsk3.c', BOUND_FILES, partial(build_python_bindings,
                                                      initialization=''))
    nenv.Depends('tsk/pytsk3.c', 'class_parser.py')

    nenv.PythonModule("pytsk3", ['tsk/pytsk3.c', 'tsk/tsk3.c'], LIBS=[env.libaff4_so, 'tsk3'])

if utils.HEADERS.get('HAVE_REGFI') and nenv.config.REGFI_HEADER_LOCATION:
    BOUND_FILES = Split("""
    %(REGFI_HEADER_LOCATION)s/regfi.h
    regfi/pyregfi.h
    """ % nenv.config.__dict__)

    nenv.Append(CFLAGS = " -I %(REGFI_HEADER_LOCATION)s " % nenv.config.__dict__)

    nenv.Command('regfi/pyregfi.c', BOUND_FILES, partial(build_python_bindings,
                                                         initialization='pyregfi_init();'))
    nenv.Depends('regfi/pyregfi.c', 'class_parser.py')

    nenv.PythonModule("pyregfi", ['regfi/pyregfi.c', 'regfi/regfi.c'], LIBS=[env.libaff4_so, 'regfi'])

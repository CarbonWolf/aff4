import os, os.path, pdb
import SconsUtils.utils as utils

Import('env')

nenv = env.Clone()

BOUND_FILES = Split("""#include/aff4_constants.h  #include/aff4_utils.h
              #include/aff4_rdf.h
              #include/aff4_io.h #include/aff4_resolver.h
              #include/ewfvolume.h #include/aff4_rdf_serialise.h
              #include/aff4_objects.h""")

import class_parser

def build_python_bindings(target, source, env):
    """ A command to generate python bindings """
    module_name = os.path.splitext(os.path.basename(target[0].name))[0]
    utils.warn("Generating automatic python bindings for module %s" % module_name)

    p = class_parser.parser(class_parser.Module(module_name), verbosity=env['V'])
    for s in source:
        p.parse(s.get_abspath())

    fd = open(target[0].get_abspath(), 'w')
    p.write(fd)
    fd.close()

nenv.Append(CFLAGS = " -include lib/config.h ")

if not nenv.get("disable_curl"):
    BOUND_FILES.append("#include/aff4_http.h")

nenv.Command('pyaff4.c', BOUND_FILES, build_python_bindings)
nenv.Depends('pyaff4.c', 'class_parser.py')

nenv.PythonModule("pyaff4", ["pyaff4.c"],LIBS="aff4")
